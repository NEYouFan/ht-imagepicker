apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

repositories {
    maven { url "http://mvn.hz.netease.com/artifactory/libs-snapshots/" }
    maven { url "http://mvn.hz.netease.com/artifactory/libs-releases/" }
    jcenter()
}

android {
    compileSdkVersion COMPILE_SDK_VERSION.toInteger()
    buildToolsVersion BUILD_TOOLS_VERSION
    resourcePrefix "ne_"

    defaultConfig {
        minSdkVersion MIN_SDK_VERSION.toInteger()
        targetSdkVersion TARGET_SDK_VERSION.toInteger()
        versionCode VERSION_CODE.toInteger()
        versionName VERSION_NAME
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

//    sourceSets {
//        main {
//            manifest.srcFile 'AndroidManifest.xml'
//            java.srcDirs = ['src']
//            resources.srcDirs = ['src']
//            aidl.srcDirs = ['src']
//            res.srcDirs = ['res']
//            assets.srcDirs = ['assets']
//            jniLibs.srcDirs = ['libs']
//        }
//    }

    lintOptions {
        abortOnError false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

dependencies {
    testCompile "junit:junit:${JUNIT_VERSION}"
    compile "com.android.support:appcompat-v7:${APPCOMPAT_V7_VERSION}"
}

def siteUrl = 'https://github.com/NEYouFan/ht-imagepicker-android'      /*
 * Copyright by Netease (c) 2016.
 * This source code is licensed under the MIT-style license found in the LICENSE file
 *  in the root directory of this source tree.
 */      // 项目的主页
def gitUrl = 'https://github.com/NEYouFan/ht-imagepicker-android.git'   // Git仓库的url

version = '1.0.0'
group = 'com.netease.hearttouch'
archivesBaseName='ht-imagepicker'

install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom.artifactId ='ht-imagepicker'
        pom.project {
            packaging 'aar'
            // Add your description here
            name 'ht-imagepicker 是一种支持本地图片选择、拍照、图片预览的控件。'  //项目描述
            url siteUrl
            // Set your license
            licenses {
                license {
                    name 'MIT'
                    url 'http://www.opensource.org/licenses/mit-license.php'
                }
            }
            developers {
                developer {
                    id 'zyl06'       //填写开发者基本信息
                    name 'zyl06'
                    email 'playfair@126.com'
                }
            }
            scm {
                connection gitUrl
                developerConnection gitUrl
                url siteUrl
            }
        }

    }
}
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}
task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    failOnError false
}
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

artifacts {
    archives javadocJar
    archives sourcesJar
}
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    configurations = ['archives']
    pkg {
        repo = "maven"  //发布到Bintray的那个仓库里，默认账户有四个库，我们这里上传到maven库
        name = "ht-imagepicker"  //发布到Bintray上的项目名字
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["MIT"]
        publish = true
    }
}